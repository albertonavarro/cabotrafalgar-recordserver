

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
        classpath('se.transmode.gradle:gradle-docker:1.2')
    }
}

plugins {
    id 'org.detoeuf.swagger-codegen' version '1.6.2'
}

apply plugin: 'application'
apply from: 'docker.gradle'
apply plugin: 'spring-boot'
apply from: 'swagger.gradle'

bootRun {
    systemProperties System.properties
}

bootRepackage {
    mainClass = 'com.navid.recordserver.springboot.RecordServerSpringboot'
}

springBoot {
    mainClass = 'com.navid.recordserver.springboot.RecordServerSpringboot'
}

configurations {
    all*.exclude group: '', module: 'servlet-api'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile("org.springframework.boot:spring-boot-starter-actuator")
    runtime 'org.springframework.boot:spring-boot-starter-jetty'
    compile 'com.google.guava:guava:18.0'
    compile project(':logic')
    compile project(':persistence-api')
    compile 'javax.servlet:javax.servlet-api:3.0.1'

    testCompile 'org.springframework:spring-test'
    testCompile 'org.mock-server:mockserver-netty:3.0'
    testCompile 'org.testng:testng:6.8.8'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'com.squareup.retrofit2:retrofit:2.1.0'

    runtime 'org.apache.cxf:cxf-rt-frontend-jaxws:3.0.0'
    runtime 'org.apache.cxf:cxf-rt-frontend-jaxrs:3.0.0'
    runtime 'org.apache.cxf:cxf-rt-management:3.0.0'
    runtime 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider'
    runtime 'com.fasterxml.jackson.core:jackson-databind'
    runtime 'com.fasterxml.jackson.core:jackson-core'
    runtime 'com.fasterxml.jackson.core:jackson-annotations'

    runtime 'com.navid.trafalgar:shipModelY:1.2'
    runtime 'com.navid.trafalgar:shipModelZ:1.2'
    compile 'ch.qos.logback:logback-classic:1.1.7'
    compile 'ch.qos.logback:logback-core:1.1.7'
    swaggerApi (group:"com.navid.record-server", name: 'contract', version:"${supported_api_version}", ext:"yml")
    compile 'io.logz.logback:logzio-logback-appender:1.0.11'

}

def ci_branch = "UNSET"
def ci_commit = "UNSET"
def ci_system = "LOCAL"
def ci_commit_short = "UNSET"
def ci_build_number = 0
if (System.getenv("TRAVIS")) {
    ci_branch = System.getenv("TRAVIS_BRANCH")
    ci_commit = System.getenv("TRAVIS_COMMIT")
    ci_system = "travis"
    ci_commit_short = System.getenv("TRAVIS_COMMIT")
    ci_build_number = System.getenv("TRAVIS_BUILD_NUMBER")
}

version = "${ci_system}_${ci_build_number}"

processResources {
    filesMatching('**/application.properties') {
        expand( COMMIT: ci_commit,
                BRANCH: ci_branch,
                COMMIT_SHORT: ci_commit_short,
                PIPELINE_COUNTER: ci_build_number,
                VERSION: project.version,
                SUPPORTED_API: project.supported_api_version
        )
    }
}
